#( 

# Creating Audio Corpora

This tutorial shows how to create a simple corpus based on the long-term loudness, and then load and use the corpus.

)#
## output path for corpus
$corpuspath = './mycorpus.llll';
## create corpus if it doesn't exist yet
if $corpuspath.exists() == 0 then (
    ## corpus files
    $files = getmediafiles('audio');
    ## create new corpus
    buildcorpus(
        @files $files
        @descriptors (
            ## analyze every file in terms of its long-term loudness
            larm() 
        ) 
        ## onset-based segmentation
        @segmode 1
        ## set output path
        @outpath $corpuspath
    );
    print('Corpus created. 👌🏽') 
) else (
    ## load pre-existing corpus
    setcorpus($corpuspath) 
);
## query corpus using SQLite3 syntax
$buffers = query('SELECT * FROM buffers ORDER BY larm');
$onset = 0;
## transcribe buffer results from query
for $buff in $buffers do (
    $buff.transcribe(
        @onset $onset @pan rand() 
    );
    $onset += 33
);
## trigger rendering
render(
    @play 1 @process normalize() 
)