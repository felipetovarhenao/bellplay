#( 
    --------------------------------
    AUDIO CORPORA
    --------------------------------
    One of the core features of bellplay~ is the ability to handle audio corpora â€” i.e., large collections of audio data. 
    The first step is to create an audio corpus, which will be covered in a later tutorial. 
    These audio corpora, which are saved as .llll files, can then be loaded into our scripts. 
    In turn, this makes it possible to make queries on a corpus, in SQLite3 syntax, to retrieve buffers according to certain criteria.

    This tutorial shows how to use a pre-existing corpus to retrieve buffers with well-defined pitch content, order them in pitch-wise descending order, and retune each buffer to conform to a Cm7 chord.
)#

## path to pre-existing corpus file
$corpuspath = "corpus_demo.llll";

## load the corpus
setcorpus($corpuspath);

## perform a SQLite3 query on the corpus, choosing buffers with pitch salience above 0.15 
## and ordering the results by pitch, in descending order.
$buffers = query("SELECT * FROM buffers WHERE pitchmelodia_confidence > 0.15 ORDER BY pitchmelodia DESC");

## iterate through each buffer and transcribe them
for $buffer $i in $buffers do (
    
    ## evenly-spaced onset for current buffer
    $onset = ($i - 1) * 50;

    $pitch = $buffer.getkey("pitchmelodia");

    ## get pitch deviation in cents from a Cm7 chord;
    $detune = $pitch.pitchdiff(0 3 7 10);

    ## transcribe buffer, and apply deviation and hanning-shaped gain envelope
    $buffer.transcribe(
        @onset $onset
        @detune $detune
        @gain hanning()
        @pan rand()
        @pitchkey "pitchmelodia"
    )
);

## trigger rendering
render(@play 1)